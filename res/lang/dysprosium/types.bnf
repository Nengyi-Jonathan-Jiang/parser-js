type := non-void-type
type := void

__WRAP__ raw-type := qualified-identifier
__WRAP__ raw-type := builtin-type
template-params := < template-param-list >

template-param-list := template-parameter
__CHAIN__ template-param-list := template-param-list , template-parameter
template-parameter := type
template-parameter := INT-LITERAL
template-parameter := BOOL-LITERAL
template-parameter := STRING-LITERAL
template-parameter := CHAR-LITERAL
template-parameter := FLOAT-LITERAL

template-parameter := expression ( expr )

template := raw-type template-params

unique-ptr := non-void-type *
shared-ptr := non-void-type $
reference := non-void-type &
nullable-unique-ptr := non-void-type * ?
nullable-shared-ptr := non-void-type $ ?
nullable-reference := non-void-type & ?

ptr-type := unique-ptr
ptr-type := shared-ptr
ptr-type := reference
ptr-type := nullable-unique-ptr
ptr-type := nullable-shared-ptr
ptr-type := nullable-reference

mutable := non-void-type mut

unsized-array-of := non-void-type [ ]
sized-array-of := non-void-type [ expr ]

basic-type := raw-type
basic-type := template

constructible-type := basic-type
constructible-type := sized-array-of

non-constructible-type := ptr-type
non-constructible-type := mutable
non-constructible-type := unsized-array-of

non-void-type := constructible-type
non-void-type := non-constructible-type

non-void-type := ( non-void-type )